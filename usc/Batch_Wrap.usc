'********************************************************************************************
'AUTHOR: Ralph Whaite
'DATE: 08-27-2015                                                                            *
%version 01.00 08/27/2015                                                                 
'********************************************************************************************
'Based on TXBATCH
'AUTHOR: Chris Love                                                                         *
'DATE: 12-8-2004                                                                            *

start Batch_Wrap(parmfile, option, retcode)
'*********************************Variables***************************************************
'load configuration
parmfile          is x
option            is x     ' Parameters for script behavior (Separated by semicolon)
                              ' Valid Options :
retcode           is b     ' Returned code for script execution (non-zero indicates error)


SEQOUT     is x SEQOUT     = "/c0/EXPORT/carebatch/TXFile"
batchdir   is x batchdir   = "/c0/EXPORT/carebatch/"
historydir is x historydir = "/c0/EXPORT/carebatch/hx/"
jcl_file   is x jcl_file   = "/c0/EXPORT/carebatch/jcl/JCLProd.txt"
tracefile  is x tracefile  = "/c4/EXPORT2/whaiter/CareTrace.txt"
tracefile2 is x tracefile2 = "CareTrace.txt"
remote-sys is x remote-sys = "hhsibmprod.hhsc.texas.gov"
submitfile is x submitfile = "N"
COMPCODE   is x COMPCODE   = "230"
footertxt  is x              'Text String for footer record
FileNum		 is b							 'File Handle for adding footer record
CLLIST		 is x							 'Saved List ID fromt he parm file.  
cltList[]  is x							 'Client List Array from to be handed to dx-batch if CLIST is dp
transcount is i
holidays[] is x
ITnotify[] is x
notify[]   is x
allowoverwrite is x

'Misc
go            is x
today         is x today        = $today
day           is x day          = $seg(today,1,2)
month         is x month        = $seg(today,4,2)
year          is x year         = $seg(today,7,4)
holiday				is x

'User Messages
badfile_sub   is x badfile_sub  = "The batch script could not open the batched files!"
badfile_body  is x badfile_body = "The file was not available at the time the script was last run."
badftp_sub    is x badftp_sub   = "The batch script could not ftp the new files!"
badftp_body   is x badftp_body  = "FTP attempt failed. "
errmsg        is x errmsg       = "File Already Present for Today's Date -- Process Aborted. CONTACT MIS DEPT."
errmsg2       is x errmsg2      = "TXBATCH2 CANCELED!!"

'12-13-2005
'When $ftp fails one of these error messages will be added to the email body
' so we can see what the return code was quicker
linefeed      is x  linefeed  = x"0a"
ftperror      is x  ftperror  = linefeed + linefeed + "$FTP Return code was: "
ftpErrorRc[]  is x  ftpErrorRc[1] = linefeed + "1 - Login failed on remote system"
                    ftpErrorRc[2] = linefeed + "2 - Local directory set failed"
                    ftpErrorRc[3] = linefeed + "3 - Remote directory set failed"
                    ftpErrorRc[4] = linefeed + "4 - Invalid ftp command issued"
                    ftpErrorRc[5] = linefeed + "5 - File name specified doesn't exist"
                    ftpErrorRc[6] = linefeed + "6 - Zero length file transferred (warning only; process is completed normally)"
                    ftpErrorRc[7] = linefeed + "7 - The connection was refused by the remote system"

'Return Codes
rc 			  is i 'ftp
rc2 		  is i 'unix commands
rc3           is i 'filecheck
rc4           is i '$misprog
rc5           is i '2nd checkfile
ftp           is i 'Check to make sure username/password is good

'We will need a tempfile for some unix commands later
tempfile      is x  rc = $tempfile(tempfile)

'Unix Commands
cmd           is x 'generic $unix command
fold          is x fold   = "fold -bw80 " + SEQOUT + " > " + tempfile + " ; cp " + tempfile + " " + SEQOUT 
catjcl        is x catjcl = "(echo | cat " + jcl_file + " " + SEQOUT + ") > " + tempfile + " ; cp " + tempfile + " " + SEQOUT 
archive       is x archive = "mv " + SEQOUT + " " + historydir + "TXFile" + day + month + year
perl          is x perl = "cd " + batchdir + ";perl -p -e 's/\n/\r\n/' < " + tracefile2 + " > Tempfile;mv Tempfile " + tracefile2
                   'Example  perl -p -e 's/\n/\r\n/' < CareTrace.txt > Tempfile;mv Tempfile CareTrace.txt

'Prescripts and Postscripts
Prescript[]   is x
PreCount      is i
c							is i
Postscript[]   is x
PostCount      is i
remove_footer  is x
prog_type      is x

'TXBATCH Parameters
prog					is x  prog = "TXBATCH5"

'FTP
win           is i
k             is i
ln            is i
temppath      is x
list[]        is x
ftpcmd[]      is x
care_id       is x
care_pass     is x
login-id      is x
login-passwd  is x
remote-dir    is x

'set defaults
remove_footer = "Y"
prog_type = "MIS"
allowoverwrite = "N"

getParm(parmfile)
getOption(option)

'Turn on our tracefile if indicated by DP in the parmfile
if tracefile dp
	$trace("path.on", tracefile)
endif

'Check if our TXFile is still in the carebatch directory, we don't want to accidetly over write a batch file that has not been
'submitted and subsequently archived.
rc3 = $checkfile(SEQOUT)
if rc3 < 1 and allowoverwrite != "Y" then
 if $operioallow = "Y" then
   $disp(errmsg,17,5,80,"H")
   $acpt()
 endif
 return
endif

'Check for Batch Holiday.  If yes, display message and stop processing
rc = $find($today,holidays[],1,"F")
if rc > 0
 if $operioallow = "Y" then
	$disp("STOP - Batch Holiday ",17,5,21) 
	$disp(Holiday,17,26,22,"H")
	$disp("Press Any key to Cancel", 19,10)
	$acpt()
 endif
	return
endif

'Now on to processing the batch

'Test for the FTP User ID and Password and make sure they are valid by doing a quick FTP command.
if $operioallow = "Y" then
   $disp(`"Version: " + $scriptver`,2,65,15,"H")
   $disp("Please enter Care User ID: ",17,5,32,"H")
   $acpt(care_id,17,35,8)
   $disp("Please enter Care Password: ",18,5,33,"H")
   $acpt(care_pass,18,35,8)
   $disp("Complete Process?  (Y/N)",20,5,30,"H")
   $acpt(go,20,33,1)
   if go != "Y" and go != "y" then
      return
   endif
endif

login-id = care_id
login-passwd = care_pass

if login-id !dp or login-passwd !dp then
   if $operioallow = "Y" then
      $erasewin(0)
      $disp("NO USERNAME OR PASSWORD ENTERED!!! ",17,5,32,"H")
      $acpt()
   endif
   return
endif

if $operioallow = "Y" then
   'Check if the username/password is still good
   $disp("Testing Password/Username",22,10,30,"H")
endif

if submitfile = "Y"
   ftp = $ftp(ftpcmd[4], remote-sys, login-id, login-passwd, batchdir, remote-dir)  
else
   ftp = 0
endif

if ftp = 1
   if $operioallow = "Y" then
      $erasewin(0)
      $disp("BAD USERNAME OR PASSWORD ENTERED!!! ",17,5,32,"H")
      $acpt()
   endif
   return 
endif 
 
'See if a list ID was defined in the parfile.  If yes, put that client list into an array to hand to dx-batch.
 if CLLIST dp
 	rc = $getlist(cltlist[],2,CLLIST)
 endif

 'See if any Prescripts and defined and call them in order
 $disp("Running Pre-Processing Scripts",22,10,30,"H")
 c = 0
 PreCount = $maxarray(PreScript[])
 $trace("off")
 do while c++ < PreCount
  call PreScript[c](parmfile,option,cltlist[],retcode)
 enddo
 $trace("on") 

 'update the CLLIST with anything the PreScripts might have changed
 if CLLIST dp then
    rc = $putlist(cltlist[], 2, CLLIST, ,"R")
 endif

  select Prog_type
   case "MIS"
      rc4 = $misprog(prog,parmfile)
   case "USC"
      call prog (parmfile, option, cltlist[], rc4)
  endselect

  rc5 = $checkfile(SEQOUT)

  if rc5 > 0 then
     $erasewin(0)
     $disp(errmsg2,22,5,50,"H")
     $acpt() 
     return
  endif

  (void)$unix(fold)

   if remove_footer = "Y" then
      remove-footer(seqout) 'this function removes the footer record
   endif
   transcount = get-transcount(seqout)  'This functions gets the transaction count from the file 
	
   option += "transcount`" + $castx(transcount) + ";"
   option += ";batchfilename`" + SEQOUT + ";"


'See if any Prescripts and defined and call them in order
 c = 0
 PostCount = $maxarray(PostScript[])
 $trace("off")
 do while c++ < PostCount
 	call PostScript[c](parmfile,option,cltlist[],retcode)
 enddo
 $trace("on")
  
 'reparse the option to get updates from post-scripts
 getoption(option)

 put-footer(compcode, transcount, seqout) 'add back the footer with the new transcount

 (void)$unix(catjcl)

ftpcmd[1] = "as"
ftpcmd[2] = "filetype=jes"
ftpcmd[3] = "put " + SEQOUT
ftpcmd[4] = "quit"

if submitfile = "Y"
   rc = $ftp(ftpcmd[], remote-sys, login-id, login-passwd, batchdir, remote-dir)
else
   rc = 0
endif

'If the ftp fails then it will return something other than zero
'When that happens email MIS and the person who's running the script
if rc > 0
   if $maxarray(ITNotify[]) > 0 then
      badftp_body += ftperror + ftpErrorRc[rc]
      rc = $email(ITnotify[], badftp_sub, badftp_body)
   endif
   return
endif

(void)$unix(archive)

 'run perl command to fix carriage returns.  This is helpful if the tracefile is sent to the end user.
 'It fixes the file so that it will display properly in Notepad.  Most end users do not have
 'Boxer or UltraEdit
 (void)$unix(perl)

'Combine the notify and IT notify array of email addresses to send everyone the success message.
rc = $arrpush(notify[],ITnotify[])
rc = $sortu(notify[])
 
if $maxarray(notify[]) > 0
  c = 0
  do while c++ < $maxarray(notify[])
    cmd = "(cd " + batchdir + ";Echo CareBatch TraceFile;"
    cmd += "uuencode " + tracefile2 + " " + tracefile2 +  ") |mail -s 'CareBatch TraceFile' " + notify[c]
    (void)$unix(cmd)
  enddo
endif

$trace("off")

end Batch_Wrap 

%include inc_GetParm
%include inc_GetOption

function get-transcount(Legacy-Batch) is i

legacy-batch is x

rc          is i
LastLine    is x
cmd        is x


cmd = "tail -n 1 " + Legacy-Batch

rc = $unix(cmd,LastLine)

LastLine = $seg(LastLine,8,6)

get-transcount = $casti(LastLine) + 1

end get-transcount

dynamic function remove-footer(legacy-batch) is i

legacy-batch   is x
cmd            is x

rc             is b
tempfile       is x

rc = $tempfile(tempfile)

cmd = "sed '$d' " + Legacy-Batch + " > " + tempfile + " ; cp " + tempfile + " " + Legacy-Batch

remove-footer = $unix(cmd)

end remove-footer

dynamic function put-footer(comp-code, trans-count, batch-file) is i
comp-code   is x
trans-count is i
batch-file  is x
cmd         is x

cmd = "echo " + x"22" + COMP-CODE + "2A" + $formatf(trans-count, "999999") + x"22" + " >> " + batch-file
put-footer = $unix(cmd)

end put-footer
